// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Memory.jack

/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */
class Memory {

    // When the jack compiler compiles memory[x],
    // it adds memory to x, pops it into pointer 1,
    // then uses that value to access the RAM location
    // at which pointer 1 points.
    // Hence if memory = 0, any location x in RAM can be
    // accessed with memory[x];
    static Array memory;

    static Array freeList;

    static int HEAP_BASE, HEAP_SIZE, length, next;

    /** Initializes the class. */
    function void init() {
        let memory = 0;

        let HEAP_BASE = 2048;
        let HEAP_SIZE = 14335;

        let length = 0;
        let next = 1;

        let freeList = HEAP_BASE;
        let freeList[length] = HEAP_SIZE;
        let freeList[next] = null;

        return;
    }

    /** Returns the RAM value at the given address. */
    function int peek(int address) {
        return memory[address];
    }

    /** Sets the RAM value at the given address to the given value. */
    function void poke(int address, int value) {
        let memory[address] = value;
        return;
    }

    /** Finds an available RAM block of the given size and returns
     *  a reference to its base address. */
    function int alloc(int size) {
        var Array block, prevBlock, nextBlock;

        let block = freeList;
        let prevBlock = 0;

        while (block[length] < size) {
            if (block[next] = null) {
                return -1;
            }
            let prevBlock = block;
            let block = block[next];
        }

        let nextBlock = block + size + 2;
        let nextBlock[length] = block[length] - size - 2;
        let nextBlock[next] = block[next];

        let block[length] = size;
        let block[next] = null;

        if (block = freeList) {
            let freeList = nextBlock;
        } else {
            let prevBlock[next] = nextBlock;
        }

        return block + 2;
    }

    /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations. */
    function void deAlloc(Array o) {
        // set restored block to be start of freeList
        let memory[o - 1] = memory[freeList + 1];
        let freeList = o - 2;
        return;
    }
}
