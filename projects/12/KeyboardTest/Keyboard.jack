// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    static int LOC;

    /** Initializes the keyboard. */
    function void init() {
        let LOC = 24576;
        return;
    }

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newLine()
     * backspace = 129 = String.backSpace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(LOC);
    }

    /**
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character
     * of the pressed key.
     */
    function char readChar() {
        var int key;

        // local variables are auto-initialized to 0
        while (key = 0) {
            let key = Keyboard.keyPressed();
        }

        // wait until release or press another key
        while (Keyboard.keyPressed() = key) {}

        do Output.printChar(key);

        return key;
    }

    /**
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String string;
        var char key;

        do Output.printString(message);

        let string = String.new(80);
        let key = Keyboard.readChar();

        while (~(key = String.newLine())) {
            if (~(key = String.backSpace())) {
                do string.appendChar(key);
            } else {
                if (string.length() > 0) {
                    do string.eraseLastChar();
                }
            }

            let key = Keyboard.readChar();
        }

        return string;
    }

    /**
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces.
     */
    function int readInt(String message) {
        var String string;
        var char key;

        do Output.printString(message);

        let string = String.new(80);
        let key = Keyboard.readChar();

        // if key is minus symbol ('-'), start with next char
        if (key = 45) {
            do string.appendChar(key);
            let key = Keyboard.readChar();
        }

        while ((key > 47) & (key < 58)) {
            if (~(key = String.backSpace())) {
                do string.appendChar(key);
            } else {
                if (string.length() > 0) {
                    do string.eraseLastChar();
                }
            }

            let key = Keyboard.readChar();
        }

        return string.intValue();
    }
}
